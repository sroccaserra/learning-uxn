( macros )

~src/macros.tal

%BIT { SFT #01 AND }

( constants )

%SCREEN_WIDTH { #02c0 }
%SCREEN_HEIGHT { #01c0 }

( devices )

|00 @System     [ &vector $2 &wst $1 &rst $1 &eaddr $2 &ecode $1 &pad $1 &r $2 &g $2 &b $2 &debug $1 &halt $1 ]
|10 @Console    [ &vector $2 &read $1 &pad $5 &write $1 &error $1 ]
|20 @Screen     [ &vector $2 &width $2 &height $2 &auto $1 &pad $1 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1 ]
|a0 @File       [ &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2 ]

( zero page )

|00

( init )

|0100 @program
    ( theme )
    #3a5d .System/r DEO2
    #3acd .System/g DEO2
    #3aad .System/b DEO2

    SCREEN_WIDTH .Screen/width DEO2
    SCREEN_HEIGHT .Screen/height DEO2

    ;file-name .File/name DEO2
    ;parse-header JSR2
    ;set-pixel ;parse-data JSR2
BRK

@parse-header ( -- w* h* )
    #0003 .File/length DEO2
    ;&buffer .File/read DEO2
    ;&buffer LDA2 LIT2 "P4 NEQ2 ,&wrong-magic-number JCN

    #0001 .File/length DEO2
    ;parse-number JSR2
    ;parse-number JSR2
RTN
&buffer $4
&wrong-magic-number
    ;error-wrong-magic-number ;print-str JSR2 ;&buffer ;print-str JSR2 LF
HALT BRK

@parse-number ( -- number* )
    #0000
    &loop
        ;get-char JSR2
        DUP LIT '0 < ,&end JCN
        LIT '0 - TOS
        SWP2 #000a **
        ADD2
        ,&loop JMP

        &end
        POP
RTN

@get-char ( -- char^ )
    ;&char .File/read DEO2
    LIT &char $1
RTN

@parse-data ( w* h* set-pixel-fn* -- )
    ;&set-pixel-fn STA2

    ( w* h* )
    SWP2 #0007 ADD2 #0008 DIV2 ,&nb-chars-by-row STR2

    ( h* )
    &loop-y
        #0000 ,&nb-char STR2
        LIT2 &nb-chars-by-row $2
        &loop-chars
            ;get-char JSR2 STH
            #0008 #0000
            &loop-bits
                ( ... nb-bit* )
                DUP2 LIT2 &nb-char $2 #0008 MUL2 ADD2 ( ... nb-bit* x* )
                OVR2 LIT2 &y $2 ( ... nb-bit* x* nb-bit* y* )
                SWP2 NIP 8MOD #07 SWP - STHrk SWP BIT
                ( ... nb-bit* x* y* value^ ) ;&set-pixel JSR2
                INC2 NEQ2k ,&loop-bits JCN
                POP2 POP2
            POPr
            ,&nb-char LDR2 INC2 DUP2 ,&nb-char STR2
            OVR2 NEQ2 ,&loop-chars JCN
            POP2

        ,&y LDR2 INC2 DUP2 ,&y STR2
        ( h* y* )
        OVR2 NEQ2 ,&loop-y JCN
        POP2
RTN
&set-pixel LIT2 &set-pixel-fn $2 JMP2

@set-pixel ( x* y* value^ -- )
    STH
    .Screen/y DEO2
    .Screen/x DEO2
    STHr .Screen/pixel DEO
RTN

~src/common.tal

@file-name "images/count_8.pbm 00
@error-wrong-magic-number "Error: 20 "wrong 20 "magic 20 "number 20 00
